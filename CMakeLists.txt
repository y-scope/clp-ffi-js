cmake_minimum_required(VERSION 3.16)

if(NOT CMAKE_TOOLCHAIN_FILE)
    set(CMAKE_TOOLCHAIN_FILE
        "${CMAKE_CURRENT_SOURCE_DIR}/build/emsdk/upstream/emscripten/cmake/Modules/Platform/\
Emscripten.cmake"
    )
endif()

# Extract the project name & version from package.json
if(EXISTS "${CMAKE_CURRENT_SOURCE_DIR}/package.json")
    file(READ "${CMAKE_CURRENT_SOURCE_DIR}/package.json" CLP_FFI_JS_PACKAGE_JSON_CONTENT)
else()
    message(FATAL_ERROR "`package.json` not found in ${CMAKE_CURRENT_SOURCE_DIR}")
endif()
if("${CLP_FFI_JS_PACKAGE_JSON_CONTENT}" MATCHES "\"name\":[ ]*\"([^\"]+)\"")
    set(CLP_FFI_JS_PROJECT_NAME "${CMAKE_MATCH_1}")
else()
    set(CLP_FFI_JS_PROJECT_NAME "clp-ffi-js")
endif()
if("${CLP_FFI_JS_PACKAGE_JSON_CONTENT}" MATCHES "\"version\":[ ]*\"([^\"]+)\"")
    set(CLP_FFI_JS_VERSION "${CMAKE_MATCH_1}")
else()
    set(CLP_FFI_JS_VERSION "0.0.0")
endif()

project(
    "${CLP_FFI_JS_PROJECT_NAME}"
    LANGUAGES
        C
        CXX
    VERSION "${CLP_FFI_JS_VERSION}"
)

# Enable exporting compile commands
set(CMAKE_EXPORT_COMPILE_COMMANDS
    ON
    CACHE BOOL
    "Enable/Disable output of compile commands during generation."
    FORCE
)

if(${CLP_FFI_JS_PROJECT_NAME}_IS_TOP_LEVEL)
    # Include dependency settings if the project isn't being included as a subproject.
    # NOTE: We mark the file optional because if the user happens to set the necessary dependency
    # location variables, this file is not necessary.
    include("build/deps/cmake-settings/settings.cmake" OPTIONAL)
endif()

# Disable response files since `clang-tidy` doesn't seem to be able to use them
set(CMAKE_CXX_USE_RESPONSE_FILE_FOR_INCLUDES OFF)

# Set the default build type to Release if not specified
if(NOT CMAKE_BUILD_TYPE)
    set(CLP_FFI_JS_DEFAULT_BUILD_TYPE "Release")
    message(STATUS "No build type specified. Setting to '${CLP_FFI_JS_DEFAULT_BUILD_TYPE}'.")
    set(CMAKE_BUILD_TYPE
        "${CLP_FFI_JS_DEFAULT_BUILD_TYPE}"
        CACHE STRING
        "Choose the type of build."
        FORCE
    )
endif()

set(CMAKE_EXECUTABLE_SUFFIX ".js" CACHE STRING "Binary type to be generated by Emscripten.")

# Set up common compile and link options to be merged with other options as necessary.
set(CLP_FFI_JS_COMMON_COMPILE_OPTIONS
    -fwasm-exceptions
)
set(CLP_FFI_JS_COMMON_LINK_OPTIONS
    -fwasm-exceptions
    -sALLOW_MEMORY_GROWTH
    -sEXPORT_ES6
    -sEXPORTED_RUNTIME_METHODS=["HEAPU8"]
    -sMAXIMUM_MEMORY=4GB
    -sMODULARIZE
    -sWASM_BIGINT
)
if(CMAKE_BUILD_TYPE MATCHES "Release")
    list(APPEND CLP_FFI_JS_COMMON_COMPILE_OPTIONS
        -flto
    )
    list(APPEND CLP_FFI_JS_COMMON_LINK_OPTIONS
        -flto
        --closure=1
    )
endif()

# Save the compiler's extra arguments for use in `clang-tidy` and other tools.
execute_process(
        COMMAND ${CMAKE_CXX_COMPILER}
        ${CLP_FFI_JS_COMMON_COMPILE_OPTIONS}
        --cflags
        COMMAND_ERROR_IS_FATAL ANY
        OUTPUT_FILE "${CMAKE_BINARY_DIR}/compiler-extra-args.txt"
        OUTPUT_STRIP_TRAILING_WHITESPACE
)

set(CLP_FFI_JS_SRC_MAIN
    src/clp_ffi_js/ir/decoding_methods.cpp
    src/clp_ffi_js/ir/query_methods.cpp
    src/clp_ffi_js/ir/StreamReader.cpp
    src/clp_ffi_js/ir/StructuredIrStreamReader.cpp
    src/clp_ffi_js/ir/StructuredIrUnitHandler.cpp
    src/clp_ffi_js/ir/UnstructuredIrStreamReader.cpp
    src/clp_ffi_js/utils.cpp
)

set(CLP_FFI_JS_SRC_CLP_CORE
    ${CLP_FFI_JS_CLP_SOURCE_DIRECTORY}/components/core/src/clp/ffi/EncodedTextAstError.cpp
    ${CLP_FFI_JS_CLP_SOURCE_DIRECTORY}/components/core/src/clp/ffi/ir_stream/decoding_methods.cpp
    ${CLP_FFI_JS_CLP_SOURCE_DIRECTORY}/components/core/src/clp/ffi/ir_stream/ir_unit_deserialization_methods.cpp
    ${CLP_FFI_JS_CLP_SOURCE_DIRECTORY}/components/core/src/clp/ffi/ir_stream/search/ErrorCode.cpp
    ${CLP_FFI_JS_CLP_SOURCE_DIRECTORY}/components/core/src/clp/ffi/ir_stream/search/QueryHandlerImpl.cpp
    ${CLP_FFI_JS_CLP_SOURCE_DIRECTORY}/components/core/src/clp/ffi/ir_stream/search/utils.cpp
    ${CLP_FFI_JS_CLP_SOURCE_DIRECTORY}/components/core/src/clp/ffi/ir_stream/utils.cpp
    ${CLP_FFI_JS_CLP_SOURCE_DIRECTORY}/components/core/src/clp/ffi/KeyValuePairLogEvent.cpp
    ${CLP_FFI_JS_CLP_SOURCE_DIRECTORY}/components/core/src/clp/ffi/SchemaTree.cpp
    ${CLP_FFI_JS_CLP_SOURCE_DIRECTORY}/components/core/src/clp/ir/EncodedTextAst.cpp
    ${CLP_FFI_JS_CLP_SOURCE_DIRECTORY}/components/core/src/clp/ir/LogEventDeserializer.cpp
    ${CLP_FFI_JS_CLP_SOURCE_DIRECTORY}/components/core/src/clp/ReadOnlyMemoryMappedFile.cpp
    ${CLP_FFI_JS_CLP_SOURCE_DIRECTORY}/components/core/src/clp/ReaderInterface.cpp
    ${CLP_FFI_JS_CLP_SOURCE_DIRECTORY}/components/core/src/clp/streaming_compression/zstd/Decompressor.cpp
)

set(CLP_FFI_JS_SRC_ZSTD
    ${CLP_FFI_JS_ZSTD_SOURCE_DIRECTORY}/lib/common/entropy_common.c
    ${CLP_FFI_JS_ZSTD_SOURCE_DIRECTORY}/lib/common/error_private.c
    ${CLP_FFI_JS_ZSTD_SOURCE_DIRECTORY}/lib/common/fse_decompress.c
    ${CLP_FFI_JS_ZSTD_SOURCE_DIRECTORY}/lib/common/zstd_common.c
    ${CLP_FFI_JS_ZSTD_SOURCE_DIRECTORY}/lib/common/xxhash.c
    ${CLP_FFI_JS_ZSTD_SOURCE_DIRECTORY}/lib/decompress/huf_decompress.c
    ${CLP_FFI_JS_ZSTD_SOURCE_DIRECTORY}/lib/decompress/zstd_ddict.c
    ${CLP_FFI_JS_ZSTD_SOURCE_DIRECTORY}/lib/decompress/zstd_decompress_block.c
    ${CLP_FFI_JS_ZSTD_SOURCE_DIRECTORY}/lib/decompress/zstd_decompress.c
)

set(CLP_FFI_JS_SUPPORTED_ENVIRONMENTS
    node
    worker
    CACHE INTERNAL
    "List of supported environments."
)

set(CLP_BUILD_CLP_STRING_UTILS ON)
add_subdirectory(${CLP_FFI_JS_CLP_SOURCE_DIRECTORY}/components/core/src/clp/string_utils)

# The `clp_s` component is linked because the IR search feature uses the same KQL syntax as `clp_s`,
# by reusing `clp_s`'s AST and KQL libraries.
#
# However, 'clp-ffi-js' doesn't currently require other 'clp_s' build targets (e.g., executables,
# archive readers, etc.). Disabling them prevents an increase in the binary size and reduces the
# number of dependencies needed during the build process.
set(CLP_BUILD_EXECUTABLES OFF)
set(CLP_BUILD_CLP_S_ARCHIVEREADER OFF)
set(CLP_BUILD_CLP_S_ARCHIVEWRITER OFF)
set(CLP_BUILD_CLP_S_CLP_DEPENDENCIES OFF)
set(CLP_BUILD_CLP_S_IO OFF)
set(CLP_BUILD_CLP_S_JSONCONSTRUCTOR OFF)
set(CLP_BUILD_CLP_S_REDUCER_DEPENDENCIES OFF)
set(CLP_BUILD_CLP_S_SEARCH OFF)
set(CLP_BUILD_CLP_S_SEARCH_AST ON)
set(CLP_BUILD_CLP_S_SEARCH_KQL ON)
set(CLP_BUILD_CLP_S_TIMESTAMP_PARSER ON)
set(CLP_BUILD_CLP_S_TIMESTAMPPATTERN ON)
add_subdirectory(${CLP_FFI_JS_CLP_SOURCE_DIRECTORY}/components/core/src/clp_s)

find_package(Boost REQUIRED COMPONENTS headers)

set(YSTDLIB_CPP_BUILD_TESTING OFF)
add_subdirectory(${CLP_FFI_JS_YSTDLIB_SOURCE_DIRECTORY})

set(ANTLR_BUILD_SHARED OFF)
set(ANTLR_BUILD_STATIC ON)
add_subdirectory(${CLP_FFI_JS_ANTLR_RUNTIME_SOURCE_DIRECTORY})

add_subdirectory(${CLP_FFI_JS_DATE_SOURCE_DIRECTORY})
add_subdirectory(${CLP_FFI_JS_FMT_SOURCE_DIRECTORY})
add_subdirectory(${CLP_FFI_JS_SIMDJSON_SOURCE_DIRECTORY})
add_subdirectory(${CLP_FFI_JS_SPDLOG_SOURCE_DIRECTORY})

foreach(env ${CLP_FFI_JS_SUPPORTED_ENVIRONMENTS})
    set(CLP_FFI_JS_BIN_NAME "ClpFfiJs-${env}")
    add_executable(${CLP_FFI_JS_BIN_NAME})

    # Set up compile options
    target_compile_features(${CLP_FFI_JS_BIN_NAME} PRIVATE cxx_std_20)
    target_compile_definitions(${CLP_FFI_JS_BIN_NAME} PUBLIC SPDLOG_FMT_EXTERNAL=1)
    target_compile_options(${CLP_FFI_JS_BIN_NAME} PRIVATE ${CLP_FFI_JS_COMMON_COMPILE_OPTIONS})

    # Set up link options
    target_link_libraries(${CLP_FFI_JS_BIN_NAME}
        PRIVATE
        Boost::headers
        clp_s::search::ast
        clp_s::search::kql
        embind
        fmt::fmt
        spdlog::spdlog
        ystdlib::error_handling
    )
    set(CLP_FFI_JS_LINK_OPTIONS
        ${CLP_FFI_JS_COMMON_LINK_OPTIONS}
        --emit-tsd=${CLP_FFI_JS_BIN_NAME}.d.ts
        -sENVIRONMENT=${env}
    )
    target_link_options(
        ${CLP_FFI_JS_BIN_NAME}
        PRIVATE
        ${CLP_FFI_JS_LINK_OPTIONS}
    )

    message(
            "CLP_FFI_JS_BIN_NAME=\"${CLP_FFI_JS_BIN_NAME}\". \
CMAKE_BUILD_TYPE=\"${CMAKE_BUILD_TYPE}\". \
Compile options: ${CLP_FFI_JS_COMMON_COMPILE_OPTIONS}. \
Link options: ${CLP_FFI_JS_LINK_OPTIONS}."
    )

    # NOTE: We mark the include directories below as system headers so that the compiler (including
    # `clang-tidy`) doesn't generate warnings from them.
    target_include_directories(
        ${CLP_FFI_JS_BIN_NAME}
        SYSTEM
        PRIVATE
        ${CLP_FFI_JS_CLP_SOURCE_DIRECTORY}/components/core/src
        ${CLP_FFI_JS_CLP_SOURCE_DIRECTORY}/components/core/src/clp
        ${CLP_FFI_JS_NLOHMANN_JSON_SOURCE_DIRECTORY}/include
        ${CLP_FFI_JS_ZSTD_SOURCE_DIRECTORY}/lib
    )

    target_include_directories(${CLP_FFI_JS_BIN_NAME} PRIVATE src/)

    target_sources(
        ${CLP_FFI_JS_BIN_NAME}
        PRIVATE
        ${CLP_FFI_JS_SRC_MAIN}
        ${CLP_FFI_JS_SRC_CLP_CORE}
        ${CLP_FFI_JS_SRC_ZSTD}
    )
endforeach()
